# Maintainer: "Amhairghin" Oscar Garcia Amor (https://ogarcia.me)

_pkgname=kubenav
pkgname=kubenav-bin
pkgver=4.2.3
pkgrel=1
pkgdesc='Navigator for your Kubernetes clusters'
arch=('x86_64')
license=('MIT')
url='https://kubenav.io'
depends=('gtk3')
provides=('kubenav')
conflicts=('kubenav')
options=('!strip' '!emptydirs')
source=(${_pkgname}-${pkgver}.${arch}.AppImage::"https://github.com/kubenav/kubenav/releases/download/v${pkgver}/${_pkgname}-${arch}.AppImage"
        "https://github.com/kubenav/kubenav/raw/main/LICENSE"
        "${_pkgname}.desktop"
        "${_pkgname}.svg")
b2sums=('1b07176e59a39edbd657f323d03bac2903b0820c73ed1c2c9baa16a7f2d766b342f5bec0c42df4c9e21a6bf0034c8b5c82d85633d028b602640877f13e5a3734'
        '1a4a15a143799905b98f7c6bf8125e4b95f1662841d9e17ca967761a272d33f070467a6ffeba75c4a0626a1fc69b6e956227a8d46f5c883e29aebdc24ffe9439'
        '899871f9689b40a93a7a80c7210515efb24d2cb43c721d1ba6a04fb68e0fcc1569fb08db2a1c3cf7eac60c03ba28dfe91576820a7aae8f65c80e1995f77cce4d'
        'deda23bcc0fa9ec86ed5b1c1adbeacc64928abf24f3e3103d244f063169db240eae84650641932253867ab71bc5110ccdfca44ddb8d16155b4bda3511ca4becb')

prepare() {
  chmod +x "${_pkgname}-${pkgver}.${arch}.AppImage"
  "./${_pkgname}-${pkgver}.${arch}.AppImage" --appimage-extract
}

package() {
  # move the entire distribution to /usr/share
  mkdir -p "${pkgdir}"/usr/share/${_pkgname}
  mv "${srcdir}"/squashfs-root/* \
    "${pkgdir}"/usr/share/${_pkgname}

  # icon
  install -Dm 644 ${_pkgname}.svg \
    "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/${_pkgname}.svg

  # desktop file
  install -Dm 644 "${srcdir}"/${_pkgname}.desktop \
    "${pkgdir}"/usr/share/applications/${_pkgname}.desktop

  # symlink binary
  mkdir -p "${pkgdir}"/usr/bin
  ln -sf /usr/share/${_pkgname}/${_pkgname} \
    "${pkgdir}"/usr/bin/${_pkgname}

  # license
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # clean
  find "${pkgdir}" -type d -exec chmod 755 {} \;
  rm -rf "${pkgdir}"/usr/share/${_pkgname}/.DirIcon
  rm -rf "${pkgdir}"/usr/share/${_pkgname}/AppRun
  rm -rf "${pkgdir}"/usr/share/${_pkgname}/${_pkgname}.{desktop,png}
}
