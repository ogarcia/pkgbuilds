# Maintainer: "Amhairghin" Oscar Garcia Amor (https://ogarcia.me)

_latest_ok_commit=cb0da53faefcbdd81334b24a3c2290051b027712
_pkgname=mldonkey
pkgname=mldonkey-ed2kad-daemon-git
pkgver=3.2.1.r10.gcb0da53
pkgrel=1
pkgdesc="A multi-network P2P client. Daemon function, only ED2K/KAD."
arch=('aarch64' 'x86_64')
license=('GPL-2.0-or-later')
depends=('bzip2' 'file' 'libnatpmp' 'miniupnpc')
makedepends=('git' 'ocaml4-camlp4' 'ocaml4-num')
conflicts=('mldonkey')
provides=('mldonkey')
backup=('etc/conf.d/mldonkey')
source=("git+https://github.com/ygrek/mldonkey.git#commit=${_latest_ok_commit}"
        'mldonkey.service'
        'mldonkey.tmpfiles'
        'mldonkey.sysusers')
b2sums=('SKIP'
        'cc7f7794f9800d4be5ee8f31823ed382d391a0e4b7919c3c39ae9e3f90e3d726b2f6e91651decfd25c4561fe00ad696358784f01d6302fff541bc9787097f841'
        '10862789508cc6fd277a2d0e7fd53bba16a110659a9cd7df201a340fe6656f027474a5a474e39dd08266d98f21635101c3432fe9cd81e3851d248db701fee178'
        '75edfcc2f3b420449ef4c2062519bdac58156a1593ff31f3ce75c645fbc5de813fdfde799a5ed85b9796f3866d3459f22f7259526aa3b736210043587967022e')

pkgver() {
  cd "${_pkgname}"
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/release.//g'
}

build() {
  cd "${_pkgname}"
  ./configure \
    --prefix=/usr \
    --enable-minimum \
    --enable-upnp-natpmp \
    --enable-bzip2 \
    --enable-magic \
    --enable-donkeysui \
    --enable-checks \
    --disable-rpath

  make depend
  make
  make utils
}

package() {
  cd "${_pkgname}"
  make DESTDIR="${pkgdir}" install

  install -Dm755 mld_hash "${pkgdir}/usr/bin/ed2k_hash"

  install -Dm644 "${srcdir}"/mldonkey.service "${pkgdir}"/usr/lib/systemd/system/mldonkey.service
  install -Dm644 "${srcdir}"/mldonkey.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/mldonkey.conf
  install -Dm644 "${srcdir}"/mldonkey.sysusers "${pkgdir}"/usr/lib/sysusers.d/mldonkey.conf
}
